# Equality typeclass - provides equality comparison for types
#
# NOTE: Multi-arg typeclass methods are curried; application is left-associative.
import prelude as P
import type_aliases as Types

module typeclass::equality contains
  # Equality typeclass definition
  define transparent Equality as typeclass A of-kind Types::BaseType where
    eq of-type (Types::Comparator A)
  end

  define transparent equality-kind as
    Types::TypeFunction Type0

  define transparent equals as
    function A Type0 x A y A requires Equality A returns Boolean value
      eq x y
    end

  # Instance: Natural
  define transparent Equality-Natural as instance Equality Natural where
    eq as function x Natural y Natural returns Boolean value
      eq-nat-prim x y
    end
  end

  # Instance: String lives in string module to avoid import cycles.

  # Instance: Boolean
  define transparent Equality-Boolean as instance Equality Boolean where
    eq as function x Boolean y Boolean returns Boolean value
      match x of-type Boolean as ignored returns Boolean
        case Boolean::false as P::not y
        case Boolean::true as y
      end
    end
  end

  define transparent Equality-List as instance Equality (List A) where
    eq as
      function A Type0 xs (List A) ys (List A) requires Equality A returns Boolean value
        match xs of-type (List A) as ignored returns Boolean
          case List::empty as
            match ys of-type (List A) as ignored returns Boolean
              case List::empty as true
              case List::cons with h A t (List A) as false
            end
          case List::cons with hx A tx (List A) as
            match ys of-type (List A) as ignored returns Boolean
              case List::empty as false
              case List::cons with hy A ty (List A) as
                match (eq hx hy) of-type Boolean as ignored returns Boolean
                  case Boolean::false as false
                  case Boolean::true as recur A tx ty
                end
            end
        end
      end
  end

  define transparent Equality-Pair as instance Equality (Pair A A) where
    eq as
      function A Type0 left (Pair A A) right (Pair A A) requires Equality A returns Boolean value
        match left of-type (Pair A A) as ignored returns Boolean
          case Pair::pair with left-a A left-b A as
            match right of-type (Pair A A) as ignored returns Boolean
              case Pair::pair with right-a A right-b A as
                match (eq left-a right-a) of-type Boolean as ignored returns Boolean
                  case Boolean::false as false
                  case Boolean::true as eq left-b right-b
                end
            end
        end
      end
  end

end
