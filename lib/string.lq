import prelude as P
import arithmetic as Ar
import list as L
import logic as Lg
import typeclass::equality as Eq
import typeclass::order as Order
import type_aliases as Types

open Eq exposing Equality end
open Order exposing LessThan LessOrEqual GreaterThan GreaterOrEqual end
open P exposing Option::none Option::some end

module string contains
  define transparent concat as concat-string-prim
  define transparent eq as eq-string-prim

  define transparent Equality-String as instance Equality String where
    eq as eq
  end

  define transparent length as
    function s String returns Natural value
      P::length-list String (string-to-list-prim s)
    end

  define transparent nonempty-predicate as
    function s String returns Type0 value
      Lg::Not (equal Natural (length s) 0)
    end

  define transparent NonEmptyString as
    Lg::Refine String nonempty-predicate

  define transparent character-predicate as
    function c String returns Type0 value
      equal Natural (length c) 1
    end

  define transparent Character as
    Lg::Refine String character-predicate

  define transparent character as
    function s String returns Character value
      unpack (P::decide-eq-nat (length s) 1) as ok proof in
        match ok of-type Boolean as ok returns Character
          case Boolean::false as P::error Character "Character expects length 1"
          case Boolean::true as
            pack c as String in character-predicate c with s proof end
        end
      end
    end

  define transparent nonempty as
    function s String returns (P::Option NonEmptyString) value
      unpack (P::decide-eq-nat (length s) 0) as is-zero proof in
        match is-zero of-type Boolean as is-zero returns (P::Option NonEmptyString)
          case Boolean::true as Option::none NonEmptyString
          case Boolean::false as
            Option::some NonEmptyString
              (pack x as String in nonempty-predicate x with s proof end)
        end
      end
    end

  define transparent nonempty-value as
    function s NonEmptyString returns String value
      P::refine-value String nonempty-predicate s
    end

  define transparent character-string as
    function c Character returns String value
      Lg::refine-value String character-predicate c
    end

  define transparent char-code as
    function c Character returns Natural value
      char-code-prim (character-string c)
    end

  define transparent char-from-code as
    function code Natural returns Character value
      character (char-from-code-prim code)
    end

  define transparent string-from-code as
    function code Natural returns String value
      character-string (char-from-code code)
    end

  define transparent LessThan-Character as instance LessThan Character where
    lt as function x Character y Character returns Boolean value
      Order::less-than Natural (char-code x) (char-code y)
    end
  end

  define transparent LessOrEqual-Character as instance LessOrEqual Character where
    le as function x Character y Character returns Boolean value
      Order::less-or-equal Natural (char-code x) (char-code y)
    end
  end

  define transparent GreaterThan-Character as instance GreaterThan Character where
    gt as function x Character y Character returns Boolean value
      Order::greater-than Natural (char-code x) (char-code y)
    end
  end

  define transparent GreaterOrEqual-Character as instance GreaterOrEqual Character where
    ge as function x Character y Character returns Boolean value
      Order::greater-or-equal Natural (char-code x) (char-code y)
    end
  end

  define transparent char-eq as
    function a Character b Character returns Boolean value
      eq (character-string a) (character-string b)
    end

  define transparent char-is as
    function c Character s String returns Boolean value
      eq (character-string c) s
    end

  define transparent to-list as
    function s String returns List Character value
      P::map String Character character (string-to-list-prim s)
    end

  define transparent join-with as
    function sep String xs (List String) returns String value
      match xs of-type (List String) as ignored returns String
        case List::empty as ""
        case List::cons with h String t (List String) as
          match t of-type (List String) as ignored returns String
            case List::empty as h
            case List::cons with ignored String tail (List String) as
              concat (concat h sep) (recur sep t)
          end
      end
    end

  define transparent from-list as
    function xs (List Character) returns String value
      join-with "" (P::map Character String character-string xs)
    end

  define transparent hex-digit-value as
    function c Character returns (P::Option Natural) value
      let value code be char-code c in
        match (Order::less-than Natural code 48) of-type Boolean as ignored returns (P::Option Natural)
          case Boolean::true as Option::none Natural
          case Boolean::false as
            match (Order::less-or-equal Natural code 57) of-type Boolean as ignored returns (P::Option Natural)
              case Boolean::true as Option::some Natural (Ar::subtract code 48)
              case Boolean::false as
                match (Order::less-than Natural code 65) of-type Boolean as ignored returns (P::Option Natural)
                  case Boolean::true as Option::none Natural
                  case Boolean::false as
                    match (Order::less-or-equal Natural code 70) of-type Boolean as ignored returns (P::Option Natural)
                      case Boolean::true as Option::some Natural (Ar::subtract code 55)
                      case Boolean::false as
                        match (Order::less-than Natural code 97) of-type Boolean as ignored returns (P::Option Natural)
                          case Boolean::true as Option::none Natural
                          case Boolean::false as
                            match (Order::less-or-equal Natural code 102) of-type Boolean as ignored returns (P::Option Natural)
                              case Boolean::true as Option::some Natural (Ar::subtract code 87)
                              case Boolean::false as Option::none Natural
                            end
                        end
                    end
                end
            end
        end
      end
    end

  define transparent hex-pair-value as
    function first Character second Character returns (P::Option Natural) value
      match (hex-digit-value first) of-type (P::Option Natural) as ignored returns (P::Option Natural)
        case Option::none as Option::none Natural
        case Option::some with hi Natural as
          match (hex-digit-value second) of-type (P::Option Natural) as ignored returns (P::Option Natural)
            case Option::none as Option::none Natural
            case Option::some with lo Natural as
              Option::some Natural (Ar::add (Ar::mul-nat hi 16) lo)
          end
      end
    end

  define transparent decode-percent-chars as
    function first Character second Character returns String value
      match (hex-pair-value first second) of-type (P::Option Natural) as ignored returns String
        case Option::some with code Natural as string-from-code code
        case Option::none as
          concat "%" (concat (character-string first) (character-string second))
      end
    end

  define transparent url-decode-chunks as
    function chars (List Character) returns (List String) value
      match chars of-type (List Character) as ignored returns (List String)
        case List::empty as P::nil String
        case List::cons with h Character t (List Character) as
          match (char-is h "%") of-type Boolean as ignored returns (List String)
            case Boolean::true as
              match t of-type (List Character) as ignored returns (List String)
                case List::empty as P::cons String "%" (P::nil String)
                case List::cons with first Character rest1 (List Character) as
                  match rest1 of-type (List Character) as ignored returns (List String)
                    case List::empty as
                      P::cons String "%" (P::cons String (character-string first) (P::nil String))
                    case List::cons with second Character rest2 (List Character) as
                      P::cons String
                        (decode-percent-chars first second)
                        (recur rest2)
                  end
              end
            case Boolean::false as
              match (char-is h "+") of-type Boolean as ignored returns (List String)
                case Boolean::true as P::cons String " " (recur t)
                case Boolean::false as P::cons String (character-string h) (recur t)
              end
          end
      end
    end

  define transparent url-decode as
    function s String returns String value
      join-with "" (url-decode-chunks (to-list s))
    end

  define transparent drop-while-char as
    function predicate (Types::Predicate Character) xs (List Character) returns (List Character) value
      match xs of-type (List Character) as ignored returns (List Character)
        case List::empty as P::nil Character
        case List::cons with h Character t (List Character) as
          match (predicate h) of-type Boolean as ignored returns (List Character)
            case Boolean::false as P::cons Character h t
            case Boolean::true as recur predicate t
          end
      end
    end

  define transparent starts-with-chars as
    function needle (List Character) haystack (List Character) returns Boolean value
      match needle of-type (List Character) as ignored returns Boolean
        case List::empty as true
        case List::cons with n Character nt (List Character) as
          match haystack of-type (List Character) as ignored returns Boolean
            case List::empty as false
            case List::cons with h Character ht (List Character) as
              match (char-eq n h) of-type Boolean as ignored returns Boolean
                case Boolean::false as false
                case Boolean::true as recur nt ht
              end
          end
      end
    end

  define transparent list-eq-chars as
    function xs (List Character) ys (List Character) returns Boolean value
      match xs of-type (List Character) as ignored returns Boolean
        case List::empty as
          match ys of-type (List Character) as ignored returns Boolean
            case List::empty as true
            case List::cons with ignored Character ignored-tail (List Character) as false
          end
        case List::cons with x Character xt (List Character) as
          match ys of-type (List Character) as ignored returns Boolean
            case List::empty as false
            case List::cons with y Character yt (List Character) as
              match (char-eq x y) of-type Boolean as ignored returns Boolean
                case Boolean::false as false
                case Boolean::true as recur xt yt
              end
          end
      end
    end

  define transparent index-of-chars as
    function haystack (List Character) needle (List Character) offset Natural returns Natural value
      match (starts-with-chars needle haystack) of-type Boolean as ignored returns Natural
        case Boolean::true as offset
        case Boolean::false as
          match haystack of-type (List Character) as ignored returns Natural
            case List::empty as offset
            case List::cons with ignored Character t (List Character) as
              recur t needle (Ar::add offset 1)
          end
      end
    end

  define transparent index-of as
    function needle String haystack String returns Natural value
      index-of-chars (to-list haystack) (to-list needle) 0
    end

  define transparent starts-with as
    function prefix String s String returns Boolean value
      starts-with-chars (to-list prefix) (to-list s)
    end

  define transparent starts-with-any as
    function prefixes (List String) s String returns Boolean value
      P::fold String Boolean
        (function acc Boolean prefix String returns Boolean value
          match acc of-type Boolean as ignored returns Boolean
            case Boolean::true as true
            case Boolean::false as starts-with prefix s
          end
        end)
        false
        prefixes
    end

  define transparent ends-with as
    function suffix String s String returns Boolean value
      let value suffix-chars be to-list suffix in
        let value s-chars be to-list s in
          let value suffix-length be P::length-list Character suffix-chars in
            let value s-length be P::length-list Character s-chars in
              match (Order::less-than Natural s-length suffix-length) of-type Boolean as ignored returns Boolean
                case Boolean::true as false
                case Boolean::false as
                  list-eq-chars (L::drop Character (Ar::subtract s-length suffix-length) s-chars) suffix-chars
              end
            end
          end
        end
      end
    end

  define transparent has-substring as
    function needle String haystack String returns Boolean value
      match (Eq::equals Natural (length needle) 0) of-type Boolean as ignored returns Boolean
        case Boolean::true as true
        case Boolean::false as Order::less-than Natural (index-of needle haystack) (length haystack)
      end
    end

  define transparent contains-any as
    function needles (List String) haystack String returns Boolean value
      P::fold String Boolean
        (function acc Boolean needle String returns Boolean value
          match acc of-type Boolean as ignored returns Boolean
            case Boolean::true as true
            case Boolean::false as has-substring needle haystack
          end
        end)
        false
        needles
    end

  define transparent chars-less-than as
    function xs (List Character) ys (List Character) returns Boolean value
      match xs of-type (List Character) as ignored returns Boolean
        case List::empty as
          match ys of-type (List Character) as ignored returns Boolean
            case List::empty as false
            case List::cons with ignored Character ignored-tail (List Character) as true
          end
        case List::cons with x Character xt (List Character) as
          match ys of-type (List Character) as ignored returns Boolean
            case List::empty as false
            case List::cons with y Character yt (List Character) as
              let value x-code be char-code x in
                let value y-code be char-code y in
                  match (Order::less-than Natural x-code y-code) of-type Boolean as ignored returns Boolean
                    case Boolean::true as true
                    case Boolean::false as
                      match (Eq::equals Natural x-code y-code) of-type Boolean as ignored returns Boolean
                        case Boolean::true as recur xt yt
                        case Boolean::false as false
                      end
                  end
                end
              end
          end
      end
    end

  define transparent string-less-than as
    function a String b String returns Boolean value
      chars-less-than (to-list a) (to-list b)
    end

  define transparent string-less-or-equal as
    function a String b String returns Boolean value
      match (string-less-than a b) of-type Boolean as ignored returns Boolean
        case Boolean::true as true
        case Boolean::false as eq a b
      end
    end

  define transparent string-greater-than as
    function a String b String returns Boolean value
      string-less-than b a
    end

  define transparent string-greater-or-equal as
    function a String b String returns Boolean value
      match (string-greater-than a b) of-type Boolean as ignored returns Boolean
        case Boolean::true as true
        case Boolean::false as eq a b
      end
    end

  define transparent LessThan-String as instance LessThan String where
    lt as function x String y String returns Boolean value
      string-less-than x y
    end
  end

  define transparent LessOrEqual-String as instance LessOrEqual String where
    le as function x String y String returns Boolean value
      string-less-or-equal x y
    end
  end

  define transparent GreaterThan-String as instance GreaterThan String where
    gt as function x String y String returns Boolean value
      string-greater-than x y
    end
  end

  define transparent GreaterOrEqual-String as instance GreaterOrEqual String where
    ge as function x String y String returns Boolean value
      string-greater-or-equal x y
    end
  end

  define transparent substring as
    function start Natural len Natural s String returns String value
      from-list (L::take Character len (L::drop Character start (to-list s)))
    end

  define transparent split-on-chars as
    function chars (List Character)
             delim (List Character)
             delim-length Natural
             skip Natural
             current (List Character)
             segments (List (List Character))
    returns (List (List Character)) value
      match chars of-type (List Character) as ignored returns (List (List Character))
        case List::empty as P::cons (List Character) (L::reverse Character current) segments
        case List::cons with h Character t (List Character) as
          match (Eq::equals Natural skip 0) of-type Boolean as ignored returns (List (List Character))
            case Boolean::true as
              match (starts-with-chars delim chars) of-type Boolean as ignored returns (List (List Character))
                case Boolean::true as
                  let value next-segments be P::cons (List Character) (L::reverse Character current) segments in
                    let value new-skip be Ar::subtract delim-length 1 in
                      recur t delim delim-length new-skip (P::nil Character) next-segments
                    end
                  end
                case Boolean::false as
                  recur t delim delim-length 0 (P::cons Character h current) segments
              end
            case Boolean::false as
              recur t delim delim-length (Ar::subtract skip 1) current segments
          end
      end
    end

  define transparent split-on as
    function delim String s String returns List String value
      match (Eq::equals Natural (length delim) 0) of-type Boolean as ignored returns (List String)
        case Boolean::true as P::error (List String) "split-on expects a non-empty delimiter"
        case Boolean::false as
          let value delim-chars be to-list delim in
            let value delim-length be P::length-list Character delim-chars in
              let value segments-rev be
                split-on-chars (to-list s) delim-chars delim-length 0
                  (P::nil Character) (P::nil (List Character))
              in
                let value segments be L::reverse (List Character) segments-rev in
                  P::map (List Character) String from-list segments
                end
              end
            end
          end
      end
    end

  define transparent char-is-space as
    function c Character returns Boolean value
      let value s be character-string c in
        match (eq s " ") of-type Boolean as ignored returns Boolean
          case Boolean::true as true
          case Boolean::false as
            match (eq s "\n") of-type Boolean as ignored returns Boolean
              case Boolean::true as true
              case Boolean::false as
                match (eq s "\t") of-type Boolean as ignored returns Boolean
                  case Boolean::true as true
                  case Boolean::false as
                    match (eq s "\r") of-type Boolean as ignored returns Boolean
                      case Boolean::true as true
                      case Boolean::false as false
                    end
                end
            end
        end
      end
    end

  define transparent trim-left as
    function s String returns String value
      let value chars be to-list s in
        from-list (drop-while-char char-is-space chars)
      end
    end

  define transparent trim as
    function s String returns String value
      let value chars be to-list s in
        let value trimmed-front be drop-while-char char-is-space chars in
          let value reversed be L::reverse Character trimmed-front in
            let value trimmed-back be drop-while-char char-is-space reversed in
              from-list (L::reverse Character trimmed-back)
            end
          end
        end
      end
    end

  define transparent nth-char as
    function index Natural xs (List Character) returns Character value
      match xs of-type (List Character) as ignored returns Character
        case List::empty as P::error Character "char-at expects index in bounds"
        case List::cons with h Character t (List Character) as
          match (Eq::equals Natural index 0) of-type Boolean as ignored returns Character
            case Boolean::false as recur (Ar::subtract index 1) t
            case Boolean::true as h
          end
      end
    end

  define transparent char-at as
    function index Natural s String returns Character value
      nth-char index (to-list s)
    end

  define transparent head as
    function s String returns Character value
      P::head Character (to-list s)
    end

  define transparent head-nonempty as
    function s NonEmptyString returns Character value
      head (nonempty-value s)
    end

  define transparent tail as
    function s String returns String value
      from-list (P::tail Character (to-list s))
    end

  define transparent tail-nonempty as
    function s NonEmptyString returns String value
      tail (nonempty-value s)
    end

  define transparent code-in-range as
    function index Natural start Natural stop Natural returns Boolean value
      match (Order::less-or-equal Natural start index) of-type Boolean as ignored returns Boolean
        case Boolean::false as false
        case Boolean::true as Order::less-or-equal Natural index stop
      end
    end

  define transparent CharacterClass as data in Type0
    case CharacterClass::range of-type Types::BinaryFunction Natural Natural CharacterClass
  end

  define transparent class-contains-code as
    function cls CharacterClass code Natural returns Boolean value
      match cls of-type CharacterClass as ignored returns Boolean
        case CharacterClass::range with start Natural stop Natural as
          code-in-range code start stop
      end
    end

  define transparent class-contains as
    function cls CharacterClass c Character returns Boolean value
      class-contains-code cls (char-code c)
    end

  define transparent char-in-range as
    function c Character start Character stop Character returns Boolean value
      code-in-range (char-code c) (char-code start) (char-code stop)
    end

  define transparent digit-start-code as
    char-code-prim "0"

  define transparent digit-end-code as
    char-code-prim "9"

  define transparent upper-start-code as
    char-code-prim "A"

  define transparent upper-end-code as
    char-code-prim "Z"

  define transparent lower-start-code as
    char-code-prim "a"

  define transparent lower-end-code as
    char-code-prim "z"

  define transparent digit-class as
    CharacterClass::range digit-start-code digit-end-code

  define transparent upper-class as
    CharacterClass::range upper-start-code upper-end-code

  define transparent lower-class as
    CharacterClass::range lower-start-code lower-end-code

  define transparent char-is-digit as
    function c Character returns Boolean value
      class-contains digit-class c
    end

  define transparent char-is-upper as
    function c Character returns Boolean value
      class-contains upper-class c
    end

  define transparent char-is-lower as
    function c Character returns Boolean value
      class-contains lower-class c
    end

  define transparent char-is-letter as
    function c Character returns Boolean value
      let value code be char-code c in
        match (class-contains-code upper-class code)
        of-type Boolean as ignored returns Boolean
          case Boolean::true as true
          case Boolean::false as class-contains-code lower-class code
        end
      end
    end

  define transparent char-is-alnum as
    function c Character returns Boolean value
      let value code be char-code c in
        match (class-contains-code upper-class code)
        of-type Boolean as ignored returns Boolean
          case Boolean::true as true
          case Boolean::false as
            match (class-contains-code lower-class code)
            of-type Boolean as ignored returns Boolean
              case Boolean::true as true
              case Boolean::false as class-contains-code digit-class code
            end
        end
      end
    end

  define transparent char-is-word as
    function c Character returns Boolean value
      match (char-is-alnum c) of-type Boolean as ignored returns Boolean
        case Boolean::true as true
        case Boolean::false as
          match (char-is c "_") of-type Boolean as ignored returns Boolean
            case Boolean::true as true
            case Boolean::false as
              match (char-is c "-") of-type Boolean as ignored returns Boolean
                case Boolean::true as true
                case Boolean::false as char-is c ":"
              end
          end
      end
    end

  define transparent char-lt as
    function a Character b Character returns Boolean value
      Order::less-than Natural (char-code a) (char-code b)
    end

  define transparent insert-char-by as
    function cmp (Types::Comparator Character)
             x Character
             xs (List Character)
    returns (List Character) value
      match xs of-type (List Character) as ignored returns (List Character)
        case List::empty as [x]
        case List::cons with h Character t (List Character) as
          match (cmp x h) of-type Boolean as ignored returns (List Character)
            case Boolean::false as P::cons Character h (recur cmp x t)
            case Boolean::true as P::cons Character x (P::cons Character h t)
          end
      end
    end

  define transparent sort-chars-by as
    function cmp (Types::Comparator Character)
             xs (List Character)
    returns (List Character) value
      P::fold Character (List Character)
        (function acc (List Character) x Character returns (List Character) value
          insert-char-by cmp x acc
        end)
        (P::nil Character)
        xs
    end

  define transparent sort as
    function s String returns String value
      from-list (sort-chars-by char-lt (to-list s))
    end

  define transparent sort-by-length as
    function xs (List String) returns List String value
      let value cmp be
        function a String b String returns Boolean value
          Order::less-than Natural (length a) (length b)
        end
      in
        L::sort-by String cmp xs
      end
    end

  define transparent is-empty as
    function s String returns Boolean value
      Eq::equals Natural (length s) 0
    end

  define transparent is-whitespace as
    function s String returns Boolean value
      eq (trim s) ""
    end

  define transparent reverse as
    function s String returns String value
      from-list (L::reverse Character (to-list s))
    end

  define transparent lines as
    function s String returns List String value
      split-on "\n" s
    end

  define transparent unlines as
    function xs (List String) returns String value
      join-with "\n" xs
    end

  define transparent split-spaces as
    function s String returns List String value
      let value trimmed be trim s in
        P::filter String
          (function x String returns Boolean value
            P::not (is-empty x)
          end)
          (split-on " " trimmed)
      end
    end

  define transparent words as
    function s String returns List String value
      split-spaces s
    end

  define transparent unwords as
    function xs (List String) returns String value
      join-with " " xs
    end

  define transparent slice as
    function start Natural len Natural s String returns String value
      substring start len s
    end
end
