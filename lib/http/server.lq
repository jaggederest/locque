import prelude as P
import string as S
import io as IO
import file as File
import http::request as Req
import http::response as Resp
import net::tcp as TCP
import natural as N
import typeclass::equality as Eq
import type_aliases as Types

open P exposing Option::none Option::some end
open Req exposing Request end
open Resp exposing Response end

module http::server contains
  define transparent poll-micros as 100000

  define transparent log-line as
    function msg String returns computation Unit value
      IO::print (S::concat "[http] " msg)
    end

  define transparent request-summary as
    function parsed (P::Option Request) returns String value
      match parsed of-type (P::Option Request) as ignored returns String
        case Option::none as "invalid request"
        case Option::some with req Request as
          S::concat (Req::method req)
            (S::concat " " (S::concat (Req::path req) (S::concat " " (Req::version req))))
      end
    end

  define transparent response-summary as
    function resp Response returns String value
      S::concat "status " (N::to-string (Resp::status resp))
    end

  define transparent is-get-root as
    function req Request returns Boolean value
      match (Eq::equals String (Req::method req) "GET") of-type Boolean as ignored returns Boolean
        case Boolean::true as Eq::equals String (Req::path req) "/"
        case Boolean::false as false
      end
    end

  define transparent handle-request-option as
    function parsed (P::Option Request) returns Response value
      match parsed of-type (P::Option Request) as ignored returns Response
        case Option::none as Resp::bad-request ["bad request"]
        case Option::some with req Request as
          match (is-get-root req) of-type Boolean as ignored returns Response
            case Boolean::true as Resp::ok ["ok"]
            case Boolean::false as Resp::not-found ["not found"]
          end
      end
    end

  define transparent handle-request-line as
    function line String returns Response value
      handle-request-option (Req::parse-request-line line)
    end

  define transparent handle-request as
    function raw String returns Response value
      handle-request-option (Req::parse-request raw)
    end

  define transparent handle-request-compute as
    function raw String returns computation Response value
      compute
        return (handle-request raw)
      end
    end

  define transparent serve-once as
    P::and-then String Unit
      (function line String returns computation Unit value
        IO::print (Resp::render (handle-request-line line))
      end)
      IO::get-line

  define transparent serve-stdin as
    P::and-then String Unit
      (function raw String returns computation Unit value
        IO::print (Resp::render (handle-request raw))
      end)
      (File::read-file "/dev/stdin")

  define transparent serve-forever as compute
    perform (P::forever serve-once)
  end

  define transparent serve-connection-with as
    function handler (Types::Function String (computation Response)) listener Listener returns computation Unit value
      compute
        bind conn from perform (TCP::accept listener) then
          perform
            (P::bind-comp Boolean Unit
              (TCP::select-socket conn poll-micros)
              (function ready Boolean returns computation Unit value
                match ready of-type Boolean as ignored returns (computation Unit)
                  case Boolean::true as
                    P::bind-comp String Unit
                      (TCP::recv conn)
                      (function raw String returns computation Unit value
                        P::bind-comp Response Unit
                          (handler raw)
                          (function resp Response returns computation Unit value
                            P::and-then Unit Unit
                              (function ignored Unit returns computation Unit value
                                TCP::close conn
                              end)
                              (TCP::send conn (Resp::render resp))
                          end)
                      end)
                  case Boolean::false as TCP::close conn
                end
              end))
        end
      end
    end

  define transparent serve-connection as
    function listener Listener returns computation Unit value
      serve-connection-with handle-request-compute listener
    end

  define transparent serve-step-with as
    function handler (Types::Function String (computation Response)) listener Listener returns computation Unit value
      P::bind-comp Boolean Unit
        (TCP::select-listener listener poll-micros)
        (function ready Boolean returns computation Unit value
          match ready of-type Boolean as ignored returns (computation Unit)
            case Boolean::true as serve-connection-with handler listener
            case Boolean::false as compute return Unit::tt end
          end
        end)
    end

  define transparent serve-step as
    function listener Listener returns computation Unit value
      serve-step-with handle-request-compute listener
    end

  define transparent serve-connection-logged as
    function listener Listener returns computation Unit value
      compute
        bind conn from perform (TCP::accept listener) then
          perform
            (P::and-then Unit Unit
              (function ignored Unit returns computation Unit value
                P::bind-comp Boolean Unit
                  (TCP::select-socket conn poll-micros)
                  (function ready Boolean returns computation Unit value
                    match ready of-type Boolean as ignored returns (computation Unit)
                      case Boolean::true as
                        P::bind-comp String Unit
                          (TCP::recv conn)
                          (function raw String returns computation Unit value
                            P::sequence-unit
                              [
                                (log-line (S::concat "raw: " raw))
                              , (log-line (S::concat "parsed: " (request-summary (Req::parse-request raw))))
                              , (log-line (response-summary (handle-request raw)))
                              , (TCP::send conn (Resp::render (handle-request raw)))
                              , (TCP::close conn)
                              ]
                          end)
                      case Boolean::false as
                        P::and-then Unit Unit
                          (function ignored Unit returns computation Unit value
                            TCP::close conn
                          end)
                          (log-line "no data, closing")
                    end
                  end)
              end)
              (log-line "accepted connection"))
        end
      end
    end

  define transparent serve-step-logged as
    function listener Listener returns computation Unit value
      P::bind-comp Boolean Unit
        (TCP::select-listener listener poll-micros)
        (function ready Boolean returns computation Unit value
          match ready of-type Boolean as ignored returns (computation Unit)
            case Boolean::true as serve-connection-logged listener
            case Boolean::false as compute return Unit::tt end
          end
        end)
    end

  define transparent serve-tcp-with as
    function handler (Types::Function String (computation Response)) port Natural returns computation Unit value
      P::and-then Listener Unit
        (function listener Listener returns computation Unit value
          P::forever (serve-step-with handler listener)
        end)
        (TCP::listen port)
    end

  define transparent serve-tcp as
    function port Natural returns computation Unit value
      serve-tcp-with handle-request-compute port
    end

  define transparent serve-tcp-logged as
    function port Natural returns computation Unit value
      P::and-then Listener Unit
        (function listener Listener returns computation Unit value
          P::forever (serve-step-logged listener)
        end)
        (P::tap Listener
          (function listener Listener returns computation Unit value
            log-line (S::concat "listening on port " (N::to-string port))
          end)
          (TCP::listen port))
    end
end
