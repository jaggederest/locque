import prelude as P
import comparison as C
import string as S

module list contains
  define transparent drop-until as drop-until-prim
  define transparent nth as nth-prim
  define transparent take as take-prim
  define transparent drop as drop-prim
  define transparent list-last as last-prim
  define transparent list-init as init-prim

  define transparent is-empty as
    function A Type0 xs (List A) returns Boolean value
      match xs of-type (List A) as ignored returns Boolean
        empty-case as true
        cons-case with h A t (List A) as false
      end
    end

  define transparent length as
    function A Type0 xs (List A) returns Natural value
      P::length-list A xs
    end

  define transparent last as
    function A Type0 xs (List A) returns A value
      list-last A xs
    end

  define transparent first as
    function A Type0 xs (List A) returns A value
      P::head A xs
    end

  define transparent init as
    function A Type0 xs (List A) returns List A value
      list-init A xs
    end

  define transparent contains-string-fn as
    function item String acc Boolean x String returns Boolean value
      P::if-bool Boolean acc true (S::eq item x)
    end

  define transparent contains-string as
    function item String xs (List String) returns Boolean value
      P::fold String Boolean (contains-string-fn item) false xs
    end

  define transparent any-fn as
    function A Type0 predicate (for-all x as A to Boolean) acc Boolean x A returns Boolean value
      P::if-bool Boolean acc true (predicate x)
    end

  define transparent any as
    function A Type0 predicate (for-all x as A to Boolean) xs (List A) returns Boolean value
      P::fold A Boolean (any-fn A predicate) false xs
    end

  define transparent all-fn as
    function A Type0 predicate (for-all x as A to Boolean) acc Boolean x A returns Boolean value
      P::if-bool Boolean acc (predicate x) false
    end

  define transparent all as
    function A Type0 predicate (for-all x as A to Boolean) xs (List A) returns Boolean value
      P::fold A Boolean (all-fn A predicate) true xs
    end

  define transparent slice as
    function A Type0 start Natural len Natural xs (List A) returns List A value
      take A len (drop A start xs)
    end

  define transparent reverse as
    function A Type0 xs (List A) returns List A value
      P::fold A (List A)
        (function acc (List A) x A returns List A value
          P::cons A x acc
        end)
        (P::nil A)
        xs
    end

  define transparent concat as
    function A Type0 xs (List (List A)) returns List A value
      P::fold (List A) (List A)
        (function acc (List A) x (List A) returns List A value
          P::append A acc x
        end)
        (P::nil A)
        xs
    end

  define transparent insert-by as
    function A Type0 cmp (for-all x as A to for-all y as A to Boolean) x A xs (List A) returns List A value
      let value step be
        function acc (Pair (List A) Boolean) y A returns (Pair (List A) Boolean) value
          match acc of-type (Pair (List A) Boolean) as ignored returns (Pair (List A) Boolean)
            pair-case with built (List A) inserted Boolean as
              P::if-bool (Pair (List A) Boolean) inserted
                (P::pair (List A) Boolean
                  (P::append A built (P::cons A y (P::nil A)))
                  inserted)
                (P::if-bool (Pair (List A) Boolean) (cmp x y)
                  (P::pair (List A) Boolean
                    (P::append A built (P::cons A x (P::cons A y (P::nil A))))
                    true)
                  (P::pair (List A) Boolean
                    (P::append A built (P::cons A y (P::nil A)))
                    false))
          end
        end
      in
        let value initial be P::pair (List A) Boolean (P::nil A) false in
          let value result be P::fold A (Pair (List A) Boolean) step initial xs in
            match result of-type (Pair (List A) Boolean) as ignored returns (List A)
              pair-case with built (List A) inserted Boolean as
                P::if-bool (List A) inserted
                  built
                  (P::append A built (P::cons A x (P::nil A)))
            end
          end
        end
      end
    end

  define transparent sort-by as
    function A Type0 cmp (for-all x as A to for-all y as A to Boolean) xs (List A) returns List A value
      P::fold A (List A)
        (function acc (List A) x A returns List A value
          insert-by A cmp x acc
        end)
        (P::nil A)
        xs
    end

  define transparent lt-string-length as
    function a String b String returns Boolean value
      C::lt-nat (S::length a) (S::length b)
    end

  define transparent sort-strings-by-length as
    function xs (List String) returns List String value
      sort-by String lt-string-length xs
    end
end
