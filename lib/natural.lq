import arithmetic as Ar
import logic as Lg
import prelude as P
import string as S
import typeclass::order as Order
open P exposing Option::none Option::some end
open S exposing Character end

module natural contains
  define transparent to-peano as
    function n Natural returns List Unit value
      natural-to-peano-prim n
    end

  define transparent to-string as
    function n Natural returns String value
      nat-to-string-prim n
    end

  define transparent nonzero-predicate as
    function n Natural returns Type0 value
      Lg::Not (equal Natural n 0)
    end

  define transparent NonZero as
    Lg::Refine Natural nonzero-predicate

  define transparent positive-predicate as
    function n Natural returns Type0 value
      Lg::Not (equal Natural n 0)
    end

  define transparent Positive as
    Lg::Refine Natural positive-predicate

  define transparent nonzero as
    function n Natural returns (P::Option NonZero) value
      unpack (Lg::decide-eq-nat n 0) as is-zero proof in
        match is-zero of-type Boolean as is-zero returns (P::Option NonZero)
          case Boolean::true as Option::none NonZero
          case Boolean::false as
            Option::some NonZero
              (pack x as Natural in nonzero-predicate x with n proof end)
        end
      end
    end

  define transparent nonzero-value as
    function n NonZero returns Natural value
      Lg::refine-value Natural nonzero-predicate n
    end

  define transparent positive as
    function n Natural returns (P::Option Positive) value
      unpack (Lg::decide-eq-nat n 0) as is-zero proof in
        match is-zero of-type Boolean as is-zero returns (P::Option Positive)
          case Boolean::true as Option::none Positive
          case Boolean::false as
            Option::some Positive
              (pack x as Natural in positive-predicate x with n proof end)
        end
      end
    end

  define transparent positive-value as
    function n Positive returns Natural value
      Lg::refine-value Natural positive-predicate n
    end

  define transparent digit-value as
    function c Character returns (P::Option Natural) value
      let value code be S::char-code c in
        match (Order::less-than Natural code 48) of-type Boolean as ignored returns (P::Option Natural)
          case Boolean::true as Option::none Natural
          case Boolean::false as
            match (Order::greater-than Natural code 57) of-type Boolean as ignored returns (P::Option Natural)
              case Boolean::true as Option::none Natural
              case Boolean::false as Option::some Natural (Ar::subtract code 48)
            end
        end
      end
    end

  define transparent from-string-chars as
    function chars (List Character) acc Natural returns (P::Option Natural) value
      match chars of-type (List Character) as ignored returns (P::Option Natural)
        case List::empty as Option::some Natural acc
        case List::cons with h Character t (List Character) as
          match (digit-value h) of-type (P::Option Natural) as ignored returns (P::Option Natural)
            case Option::none as Option::none Natural
            case Option::some with d Natural as
              recur t (Ar::add (Ar::mul-nat acc 10) d)
          end
      end
    end

  define transparent from-string as
    function s String returns (P::Option Natural) value
      let value chars be S::to-list s in
        match chars of-type (List Character) as ignored returns (P::Option Natural)
          case List::empty as Option::none Natural
          case List::cons with ignored Character ignored (List Character) as
            from-string-chars chars 0
        end
      end
    end
end
