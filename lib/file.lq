import string as S

module file contains
  define transparent read-file as read-file-prim
  define transparent write-file as write-file-prim
  define transparent append-file as append-file-prim
  define transparent copy-file as copy-file-prim
  define transparent copy-tree as copy-tree-prim
  define transparent rename-path as rename-path-prim
  define transparent list-dir as list-dir-prim
  define transparent current-directory as current-directory-prim

  define transparent path-exists as path-exists-prim
  define transparent is-directory as is-directory-prim
  define transparent is-file as is-file-prim
  define transparent make-directory as make-directory-prim
  define transparent remove-file as remove-file-prim
  define transparent remove-directory as remove-directory-prim

  define transparent walk as walk-prim
  define transparent walk-filter as walk-filter-prim
  define transparent walk-path as
    function entry (Pair String Boolean) returns String value
      match entry of-type (Pair String Boolean) as ignored returns String
        case Pair::pair with path String isdir Boolean as path
      end
    end
  define transparent walk-is-directory as
    function entry (Pair String Boolean) returns Boolean value
      match entry of-type (Pair String Boolean) as ignored returns Boolean
        case Pair::pair with path String isdir Boolean as isdir
      end
    end

  define transparent walk-lq as
    function root String skip (List String) returns computation (List String) value
      walk-filter root ".lq" skip
    end

  define transparent stat as stat-prim
  define transparent stat-type as
    function info (Pair String (Pair Natural Natural)) returns String value
      match info of-type (Pair String (Pair Natural Natural)) as ignored returns String
        case Pair::pair with kind String rest (Pair Natural Natural) as kind
      end
    end
  define transparent stat-size as
    function info (Pair String (Pair Natural Natural)) returns Natural value
      match info of-type (Pair String (Pair Natural Natural)) as ignored returns Natural
        case Pair::pair with kind String rest (Pair Natural Natural) as
          match rest of-type (Pair Natural Natural) as ignored returns Natural
            case Pair::pair with size Natural mtime Natural as size
          end
      end
    end
  define transparent stat-mtime as
    function info (Pair String (Pair Natural Natural)) returns Natural value
      match info of-type (Pair String (Pair Natural Natural)) as ignored returns Natural
        case Pair::pair with kind String rest (Pair Natural Natural) as
          match rest of-type (Pair Natural Natural) as ignored returns Natural
            case Pair::pair with size Natural mtime Natural as mtime
          end
      end
    end

  define transparent read-lines as
    function path String returns computation (List String) value
      compute
        bind contents from perform (read-file path) then
          return (S::split-on "\n" contents)
        end
      end
    end

  define transparent write-lines as
    function path String lines (List String) returns computation Unit value
      compute
        perform (write-file path (S::join-with "\n" lines))
      end
    end

  define transparent append-lines as
    function path String lines (List String) returns computation Unit value
      let value suffix be S::join-with "\n" lines in
        compute
          perform
            (match (S::is-empty suffix) of-type Boolean as ignored returns (computation Unit)
              case Boolean::true as compute return tt end
              case Boolean::false as
                compute
                  bind exists from perform (path-exists path) then
                    perform
                      (match exists of-type Boolean as ignored returns (computation Unit)
                        case Boolean::false as write-file path suffix
                        case Boolean::true as
                          compute
                            bind contents from perform (read-file path) then
                              perform
                                (match (S::is-empty contents) of-type Boolean as ignored returns (computation Unit)
                                  case Boolean::true as append-file path suffix
                                  case Boolean::false as append-file path (S::concat "\n" suffix)
                                end)
                            end
                          end
                      end)
                  end
                end
            end)
        end
      end
    end
end
