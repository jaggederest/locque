# Benchmark helpers

import arithmetic as Ar
import assert as A
import io as IO
import natural as N
import prelude as P
import string as S
import time as Time
import typeclass::order as Order

module test::bench::helpers contains
  define transparent bench-warmups as 3
  define transparent bench-iterations as 10

  define transparent run-times as
    function A Type0 tokens (List Unit) action (computation A) returns computation Unit value
      let value step be
        function acc (computation Unit) ignored Unit returns computation Unit value
          P::and-then Unit Unit
            (function ignored Unit returns computation Unit value
              action
            end)
            acc
        end
      in
        P::fold Unit (computation Unit) step (compute return tt end) tokens
      end
    end

  define transparent measure-times as
    function A Type0 tokens (List Unit) action (computation A) returns computation (List Natural) value
      let value step be
        function acc (computation (List Natural)) ignored Unit returns computation (List Natural) value
          P::bind-comp (List Natural) (List Natural)
            acc
            (function collected (List Natural) returns computation (List Natural) value
              P::map-comp (Pair A Natural) (List Natural)
                (function measured (Pair A Natural) returns (List Natural) value
                  P::cons Natural (P::snd A Natural measured) collected
                end)
                (Time::measure A action)
            end)
        end
      in
        P::fold Unit (computation (List Natural)) step
          (compute return (of-type [] (List Natural)) end)
          tokens
      end
    end

  define transparent sum-list as
    function xs (List Natural) returns Natural value
      P::fold Natural Natural
        (function acc Natural x Natural returns Natural value
          Ar::add acc x
        end)
        0
        xs
    end

  define transparent min-list as
    function xs (List Natural) returns Natural value
      match xs of-type (List Natural) as ignored returns Natural
        case List::empty as P::error Natural "bench: no measurements"
        case List::cons with h Natural t (List Natural) as
          P::fold Natural Natural
            (function acc Natural x Natural returns Natural value
              Ar::min acc x
            end)
            h
            t
      end
    end

  define transparent max-list as
    function xs (List Natural) returns Natural value
      match xs of-type (List Natural) as ignored returns Natural
        case List::empty as P::error Natural "bench: no measurements"
        case List::cons with h Natural t (List Natural) as
          P::fold Natural Natural
            (function acc Natural x Natural returns Natural value
              Ar::max acc x
            end)
            h
            t
      end
    end

  define transparent stats-message as
    function label String avg Natural min Natural max Natural returns String value
      S::join-with " "
        [
          label
        , "avg-us"
        , N::to-string avg
        , "min-us"
        , N::to-string min
        , "max-us"
        , N::to-string max
        ]
    end

  define transparent measure-with-threshold as
    function A Type0 label String threshold Natural action (computation A) returns computation Unit value
      P::and-then Unit Unit
        (function ignored Unit returns computation Unit value
          P::bind-comp (List Natural) Unit
            (measure-times A (N::to-peano bench-iterations) action)
            (function timings (List Natural) returns computation Unit value
              let value count be P::length-list Natural timings in
                let value total be sum-list timings in
                  let value avg be Ar::div-nat total count in
                    let value min be min-list timings in
                      let value max be max-list timings in
                        P::sequence-unit
                          [
                            (IO::print (stats-message label avg min max))
                          , (A::assert-false
                              (S::concat label (S::concat " exceeded threshold-us " (N::to-string threshold)))
                              (Order::greater-than Natural max threshold))
                          ]
                      end
                    end
                  end
                end
            end)
        end)
        (run-times A (N::to-peano bench-warmups) action)
    end

  define transparent bench-value as
    function A Type0 label String threshold Natural val A returns computation Unit value
      measure-with-threshold A label threshold (compute return val end)
    end
  end
