import assert as A
import file as File
import string as S
import typeclass::equality as Eq

module test::file::io contains
  define transparent base-dir as "test/tmp/fs_io"

  define transparent path as
    function suffix String returns String value
      S::concat base-dir suffix
    end

  define transparent assert-path-exists as
    function target String expected Boolean returns computation Unit value
      compute
        bind ok from perform (File::path-exists target) then
          perform (A::assert-eq Boolean ok expected)
        end
      end
    end

  define transparent assert-is-file as
    function target String expected Boolean returns computation Unit value
      compute
        bind ok from perform (File::is-file target) then
          perform (A::assert-eq Boolean ok expected)
        end
      end
    end

  define transparent ensure-clean as compute
    sequence
      (File::remove-file (path "/a.txt"))
      (File::remove-file (path "/b.txt"))
      (File::remove-file (path "/c.txt"))
      (File::remove-directory (path "/dir_old"))
      (File::remove-directory (path "/dir_new"))
      (File::remove-directory (path "/dir_remove"))
    end
  end

  define transparent ensure-base as compute
    perform (File::make-directory base-dir)
  end

  define transparent write-base as compute
    perform (File::write-file (path "/a.txt") "hi")
  end

  define transparent append-and-read as compute
    bind ignored from perform (File::append-file (path "/a.txt") " there") then
      bind contents from perform (File::read-file (path "/a.txt")) then
        perform (A::assert-eq String contents "hi there")
      end
    end
  end

  define transparent copy-and-read as compute
    bind ignored from perform (File::copy-file (path "/a.txt") (path "/b.txt")) then
      bind contents from perform (File::read-file (path "/b.txt")) then
        perform (A::assert-eq String contents "hi there")
      end
    end
  end

  define transparent rename-copy as compute
    perform (File::rename-path (path "/b.txt") (path "/c.txt"))
  end

  define transparent remove-copy as compute
    perform (File::remove-file (path "/c.txt"))
  end

  define transparent rename-dir as compute
    bind ignored from perform (File::make-directory (path "/dir_old")) then
      perform (File::rename-path (path "/dir_old") (path "/dir_new"))
    end
  end

  define transparent remove-dir-recursive as compute
    bind ignored from perform (File::make-directory (path "/dir_remove")) then
      bind ignored from perform (File::write-file (path "/dir_remove/nested.txt") "x") then
        perform (File::remove-directory (path "/dir_remove"))
      end
    end
  end

  define transparent main as compute
    sequence
      ensure-clean
      ensure-base
      write-base
      append-and-read
      copy-and-read
      rename-copy
      (assert-path-exists (path "/b.txt") false)
      (assert-path-exists (path "/c.txt") true)
      (assert-is-file (path "/c.txt") true)
      remove-copy
      (assert-path-exists (path "/c.txt") false)
      rename-dir
      (assert-path-exists (path "/dir_new") true)
      (assert-is-file (path "/dir_new") false)
      remove-dir-recursive
      (assert-path-exists (path "/dir_remove") false)
    end
  end
end
