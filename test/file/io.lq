import prelude as P
import assert as A
import file as File
import string as S
import typeclass::equality as Eq

module test::file::io contains
  define transparent base-dir as "test/tmp/fs_io"

  define transparent path as
    function suffix String returns String value
      S::concat base-dir suffix
    end

  define transparent assert-path-exists as
    function target String expected Boolean returns computation Unit value
      P::bind-comp Boolean Unit
        (File::path-exists target)
        (function ok Boolean returns computation Unit value
          A::assert-eq Boolean ok expected
        end)
    end

  define transparent assert-is-file as
    function target String expected Boolean returns computation Unit value
      P::bind-comp Boolean Unit
        (File::is-file target)
        (function ok Boolean returns computation Unit value
          A::assert-eq Boolean ok expected
        end)
    end

  define transparent ensure-clean as compute
    perform (P::sequence-unit
      [
        (File::remove-file (path "/a.txt"))
      , (File::remove-file (path "/b.txt"))
      , (File::remove-file (path "/c.txt"))
      , (File::remove-directory (path "/dir_old"))
      , (File::remove-directory (path "/dir_new"))
      , (File::remove-directory (path "/dir_remove"))
      ])
  end

  define transparent ensure-base as compute
    perform (File::make-directory base-dir)
  end

  define transparent write-base as compute
    perform (File::write-file (path "/a.txt") "hi")
  end

  define transparent append-and-read as
    P::and-then Unit Unit
      (function ignored Unit returns computation Unit value
        P::bind-comp String Unit
          (File::read-file (path "/a.txt"))
          (function contents String returns computation Unit value
            A::assert-eq String contents "hi there"
          end)
      end)
      (File::append-file (path "/a.txt") " there")

  define transparent copy-and-read as
    P::and-then Unit Unit
      (function ignored Unit returns computation Unit value
        P::bind-comp String Unit
          (File::read-file (path "/b.txt"))
          (function contents String returns computation Unit value
            A::assert-eq String contents "hi there"
          end)
      end)
      (File::copy-file (path "/a.txt") (path "/b.txt"))

  define transparent rename-copy as compute
    perform (File::rename-path (path "/b.txt") (path "/c.txt"))
  end

  define transparent remove-copy as compute
    perform (File::remove-file (path "/c.txt"))
  end

  define transparent rename-dir as
    P::and-then Unit Unit
      (function ignored Unit returns computation Unit value
        File::rename-path (path "/dir_old") (path "/dir_new")
      end)
      (File::make-directory (path "/dir_old"))

  define transparent remove-dir-recursive as
    P::and-then Unit Unit
      (function ignored Unit returns computation Unit value
        P::and-then Unit Unit
          (function ignored Unit returns computation Unit value
            File::remove-directory (path "/dir_remove")
          end)
          (File::write-file (path "/dir_remove/nested.txt") "x")
      end)
      (File::make-directory (path "/dir_remove"))

  define transparent main as compute
    perform (P::sequence-unit
      [
        ensure-clean
      , ensure-base
      , write-base
      , append-and-read
      , copy-and-read
      , rename-copy
      , (assert-path-exists (path "/b.txt") false)
      , (assert-path-exists (path "/c.txt") true)
      , (assert-is-file (path "/c.txt") true)
      , remove-copy
      , (assert-path-exists (path "/c.txt") false)
      , rename-dir
      , (assert-path-exists (path "/dir_new") true)
      , (assert-is-file (path "/dir_new") false)
      , remove-dir-recursive
      , (assert-path-exists (path "/dir_remove") false)
      ])
  end
end
