import assert as A
import file as File
import list as L
import prelude as P
import string as S
import typeclass::equality as Eq

module test::file::helpers contains
  define transparent base-dir as "test/tmp/fs_helpers"

  define transparent path as
    function suffix String returns String value
      S::concat base-dir suffix
    end

  define transparent entry-path as
    function entry (Pair String Boolean) returns String value
      File::walk-path entry
    end

  define transparent entry-paths as
    function entries (List (Pair String Boolean)) returns (List String) value
      P::map (Pair String Boolean) String entry-path entries
    end

  define transparent contains-path as
    function target String entries (List (Pair String Boolean)) returns Boolean value
      L::member String target (entry-paths entries)
    end

  define transparent find-is-directory as
    function target String entries (List (Pair String Boolean)) returns Boolean value
      match entries of-type (List (Pair String Boolean)) as ignored returns Boolean
        case List::empty as false
        case List::cons with h (Pair String Boolean) t (List (Pair String Boolean)) as
          match (Eq::equals String (File::walk-path h) target) of-type Boolean as ignored returns Boolean
            case Boolean::true as File::walk-is-directory h
            case Boolean::false as recur target t
          end
      end
    end

  define transparent setup-tree as compute
    perform (P::sequence-unit
      [
        (File::remove-directory base-dir)
      , (File::make-directory base-dir)
      , (File::make-directory (path "/src"))
      , (File::make-directory (path "/src/sub"))
      , (File::write-file (path "/src/file.txt") "hello")
      , (File::write-file (path "/src/sub/nested.txt") "nested")
      , (File::remove-directory (path "/dest"))
      ])
  end

  define transparent check-lines as
    P::and-then Unit Unit
      (function ignored Unit returns computation Unit value
        P::bind-comp (List String) Unit
          (File::read-lines (path "/lines.txt"))
          (function lines1 (List String) returns computation Unit value
            P::sequence-unit
              [
                (A::assert-eq String (S::join-with "," lines1) "a,b")
              , (P::and-then Unit Unit
                  (function ignored Unit returns computation Unit value
                    P::bind-comp (List String) Unit
                      (File::read-lines (path "/lines.txt"))
                      (function lines2 (List String) returns computation Unit value
                        A::assert-eq String (S::join-with "," lines2) "a,b,c"
                      end)
                  end)
                  (File::append-lines (path "/lines.txt") ["c"]))
              ]
          end)
      end)
      (File::write-lines (path "/lines.txt") ["a", "b"])

  define transparent check-walk as
    P::bind-comp (List (Pair String Boolean)) Unit
      (File::walk (path "/src"))
      (function entries (List (Pair String Boolean)) returns computation Unit value
        P::sequence-unit
          [
            (A::assert-eq Boolean
                                                                (contains-path (path "/src/file.txt") entries)
                                                                true)
          , (A::assert-eq Boolean
                                                                  (contains-path (path "/src/sub") entries)
                                                                  true)
          , (A::assert-eq Boolean
                                                                    (contains-path (path "/src/sub/nested.txt") entries)
                                                                    true)
          , (A::assert-eq Boolean
                                                                      (find-is-directory (path "/src/sub") entries)
                                                                      true)
          , (A::assert-eq Boolean
                                                                        (find-is-directory (path "/src/file.txt") entries)
                                                                        false)
          ]
      end)

  define transparent check-copy-tree as
    P::and-then Unit Unit
      (function ignored Unit returns computation Unit value
        P::bind-comp Boolean Unit
          (File::path-exists (path "/dest/sub/nested.txt"))
          (function exists Boolean returns computation Unit value
            P::and-then Unit Unit
              (function ignored Unit returns computation Unit value
                P::bind-comp String Unit
                  (File::read-file (path "/dest/sub/nested.txt"))
                  (function contents String returns computation Unit value
                    A::assert-eq String contents "nested"
                  end)
              end)
              (A::assert-eq Boolean exists true)
          end)
      end)
      (File::copy-tree (path "/src") (path "/dest"))

  define transparent main as compute
    perform (P::sequence-unit
      [
        setup-tree
      , check-lines
      , check-walk
      , check-copy-tree
      ])
  end
end
