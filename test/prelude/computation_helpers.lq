import prelude as P
import arithmetic as Ar
import assert as A
import typeclass::equality as Eq

open A exposing assert-eq end
open Eq exposing Equality end

module test::prelude::computation_helpers contains
  define transparent assert-count-delta as
    function expected Natural action (computation Unit) returns computation Unit value
      P::bind-comp Natural Unit
        A::assertion-count
        (function before Natural returns computation Unit value
          P::and-then Unit Unit
            (function ignored Unit returns computation Unit value
              P::bind-comp Natural Unit
                A::assertion-count
                (function after Natural returns computation Unit value
                  A::assert-eq Natural (Ar::subtract after before) expected
                end)
            end)
            action
        end)
    end

  define transparent check-map-comp as
    P::bind-comp Natural Unit
      (P::map-comp Natural Natural
        (function n Natural returns Natural value
          Ar::add n 2
        end)
        (compute return 3 end))
      (function result Natural returns computation Unit value
        A::assert-eq Natural result 5
      end)

  define transparent check-bind-comp as
    P::bind-comp Natural Unit
      (P::bind-comp Natural Natural
        (compute return 4 end)
        (function n Natural returns (computation Natural) value
          compute return (Ar::add n 6) end
        end))
      (function result Natural returns computation Unit value
        A::assert-eq Natural result 10
      end)

  define transparent check-and-then as
    P::bind-comp Natural Unit
      (P::and-then Natural Natural
        (function n Natural returns (computation Natural) value
          compute return (Ar::add n 5) end
        end)
        (compute return 1 end))
      (function result Natural returns computation Unit value
        A::assert-eq Natural result 6
      end)

  define transparent check-tap as
    P::bind-comp Natural Unit
      A::assertion-count
      (function before Natural returns computation Unit value
        P::bind-comp Natural Unit
          (P::tap Natural
            (function ignored Natural returns (computation Unit) value
              A::assert-hit
            end)
            (compute return 9 end))
          (function result Natural returns computation Unit value
            P::bind-comp Natural Unit
              A::assertion-count
              (function after Natural returns computation Unit value
                P::and-then Unit Unit
                  (function ignored Unit returns computation Unit value
                    A::assert-eq Natural result 9
                  end)
                  (A::assert-eq Natural (Ar::subtract after before) 1)
              end)
          end)
      end)

  define transparent check-when-true as
    assert-count-delta 1 (P::when true A::assert-hit)

  define transparent check-when-false as
    assert-count-delta 0 (P::when false A::assert-hit)

  define transparent check-unless-true as
    assert-count-delta 0 (P::unless true A::assert-hit)

  define transparent check-unless-false as
    assert-count-delta 1 (P::unless false A::assert-hit)

  define transparent check-guard-true as
    P::guard true

  define transparent check-sequence-when-true as
    assert-count-delta 2 (P::sequence-when true [A::assert-hit, A::assert-hit])

  define transparent check-sequence-when-false as
    assert-count-delta 0 (P::sequence-when false [A::assert-hit, A::assert-hit])

  define transparent main as compute
    perform (P::sequence-unit
      [
        check-map-comp
      , check-bind-comp
      , check-and-then
      , check-tap
      , check-when-true
      , check-when-false
      , check-unless-true
      , check-unless-false
      , check-guard-true
      , check-sequence-when-true
      , check-sequence-when-false
      ])
  end
end
