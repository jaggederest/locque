# Prelude tests for iota reductions

import prelude as P
import assert as A
import typeclass::equality as Eq
import string as S
import type_aliases as Types

open A exposing assert-eq end
open Eq exposing Equality end

module test::prelude::iota contains
  define transparent Flagged as data b Boolean in Type0
    case Flagged::false of-type Flagged false
    case Flagged::true of-type Flagged true
  end

  define transparent Test as data b Boolean in Type0
    case Test::make of-type
      Types::Function
        (match b of-type Boolean as ignored returns Type0
          case Boolean::false as Natural
          case Boolean::true as String
        end)
        (Test b)
  end

  define transparent flag-false as
    Test::make false 7

  define transparent flag-true as
    Test::make true "ok"

  define transparent check-false-type as
    of-type flag-false (Test false)

  define transparent check-true-type as
    of-type flag-true (Test true)

  define transparent pick as
    function b Boolean x (Test b) returns
      (match b of-type Boolean as ignored returns Type0
        case Boolean::false as Natural
        case Boolean::true as String
      end)
    value
      match x of-type (Test b) as ignored returns
        (match b of-type Boolean as ignored returns Type0
          case Boolean::false as Natural
          case Boolean::true as String
        end)
        case Test::make with x
          (match b of-type Boolean as ignored returns Type0
            case Boolean::false as Natural
            case Boolean::true as String
          end)
        as x
      end
    end

  define transparent unreachable-flag as
    function x (Flagged true) returns Natural value
      match x of-type (Flagged true) as ignored returns Natural
        case Flagged::false as P::add-nat "bad" 1
        case Flagged::true as 0
      end
    end

  define transparent bool-branch-type as
    match true of-type Boolean as ignored returns Type0
      case Boolean::false as String
      case Boolean::true as Natural
    end

  define transparent check-bool-branch-type as
    of-type 0 bool-branch-type

  define transparent list-branch-type as
    match [1] of-type (List Natural) as ignored returns Type0
      case List::empty as String
      case List::cons with h Natural t (List Natural) as Natural
    end

  define transparent check-list-branch-type as
    of-type 0 list-branch-type

  define transparent pair-branch-type as
    match (P::pair Natural String 3 "hi") of-type (Pair Natural String) as ignored returns Type0
      case Pair::pair with a Natural b String as Natural
    end

  define transparent check-pair-branch-type as
    of-type 0 pair-branch-type

  define transparent unpack-branch-type as
    unpack (pack x as Natural in Type0 with 1 Natural end) as x y in y end

  define transparent check-unpack-branch-type as
    of-type 0 unpack-branch-type

  define transparent main as compute
    perform (P::sequence-unit
      [
        (A::assert-eq Natural (pick false flag-false) 7)
      , (A::assert-eq String (pick true flag-true) "ok")
      , (A::assert-eq Natural (unreachable-flag Flagged::true) 0)
      ])
  end
end
