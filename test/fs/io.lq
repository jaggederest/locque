import assert as A
import fs as FS
import io as IO
import string as S

module test::fs::io contains
  define transparent base-dir as "test/tmp/fs_io"

  define transparent path as
    function suffix String returns String value
      S::concat base-dir suffix
    end

  define transparent assert-path-exists as
    function target String expected Boolean returns computation Unit value
      compute
        bind ok from perform (FS::path-exists target) then
          perform (A::assert-eq Boolean ok expected)
        end
      end
    end

  define transparent assert-is-file as
    function target String expected Boolean returns computation Unit value
      compute
        bind ok from perform (FS::is-file target) then
          perform (A::assert-eq Boolean ok expected)
        end
      end
    end

  define transparent ensure-clean as compute
    sequence
      (FS::remove-file (path "/a.txt"))
      (FS::remove-file (path "/b.txt"))
      (FS::remove-file (path "/c.txt"))
      (FS::remove-directory (path "/dir_old"))
      (FS::remove-directory (path "/dir_new"))
      (FS::remove-directory (path "/dir_remove"))
    end
  end

  define transparent ensure-base as compute
    perform (FS::make-directory base-dir)
  end

  define transparent write-base as compute
    perform (IO::write-file (path "/a.txt") "hi")
  end

  define transparent append-and-read as compute
    bind ignored from perform (FS::append-file (path "/a.txt") " there") then
      bind contents from perform (IO::read-file (path "/a.txt")) then
        perform (A::assert-eq String contents "hi there")
      end
    end
  end

  define transparent copy-and-read as compute
    bind ignored from perform (FS::copy-file (path "/a.txt") (path "/b.txt")) then
      bind contents from perform (IO::read-file (path "/b.txt")) then
        perform (A::assert-eq String contents "hi there")
      end
    end
  end

  define transparent rename-copy as compute
    perform (FS::rename-path (path "/b.txt") (path "/c.txt"))
  end

  define transparent remove-copy as compute
    perform (FS::remove-file (path "/c.txt"))
  end

  define transparent rename-dir as compute
    bind ignored from perform (FS::make-directory (path "/dir_old")) then
      perform (FS::rename-path (path "/dir_old") (path "/dir_new"))
    end
  end

  define transparent remove-dir-recursive as compute
    bind ignored from perform (FS::make-directory (path "/dir_remove")) then
      bind ignored from perform (IO::write-file (path "/dir_remove/nested.txt") "x") then
        perform (FS::remove-directory (path "/dir_remove"))
      end
    end
  end

  define transparent main as compute
    sequence
      ensure-clean
      ensure-base
      write-base
      append-and-read
      copy-and-read
      rename-copy
      (assert-path-exists (path "/b.txt") false)
      (assert-path-exists (path "/c.txt") true)
      (assert-is-file (path "/c.txt") true)
      remove-copy
      (assert-path-exists (path "/c.txt") false)
      rename-dir
      (assert-path-exists (path "/dir_new") true)
      (assert-is-file (path "/dir_new") false)
      remove-dir-recursive
      (assert-path-exists (path "/dir_remove") false)
    end
  end
end
