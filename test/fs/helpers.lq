import assert as A
import fs as FS
import io as IO
import list as L
import prelude as P
import string as S

module test::fs::helpers contains
  define transparent base-dir as "test/tmp/fs_helpers"

  define transparent path as
    function suffix String returns String value
      S::concat base-dir suffix
    end

  define transparent entry-path as
    function entry (Pair String Boolean) returns String value
      FS::walk-path entry
    end

  define transparent entry-paths as
    function entries (List (Pair String Boolean)) returns (List String) value
      P::map (Pair String Boolean) String entry-path entries
    end

  define transparent contains-path as
    function target String entries (List (Pair String Boolean)) returns Boolean value
      L::contains-string target (entry-paths entries)
    end

  define transparent find-is-directory as
    function target String entries (List (Pair String Boolean)) returns Boolean value
      match entries of-type (List (Pair String Boolean)) as ignored returns Boolean
        empty-case as false
        cons-case with h (Pair String Boolean) t (List (Pair String Boolean)) as
          match (S::eq (FS::walk-path h) target) of-type Boolean as ignored returns Boolean
            true-case as FS::walk-is-directory h
            false-case as recur target t
          end
      end
    end

  define transparent setup-tree as compute
    sequence
      (FS::remove-directory base-dir)
      (FS::make-directory base-dir)
      (FS::make-directory (path "/src"))
      (FS::make-directory (path "/src/sub"))
      (IO::write-file (path "/src/file.txt") "hello")
      (IO::write-file (path "/src/sub/nested.txt") "nested")
      (FS::remove-directory (path "/dest"))
    end
  end

  define transparent check-lines as compute
    bind ignored from perform (FS::write-lines (path "/lines.txt")
      (P::cons String "a" (P::cons String "b" (P::nil String))))
    then
      bind lines1 from perform (FS::read-lines (path "/lines.txt")) then
        bind ignored from perform
          (A::assert-eq String (S::join-with "," lines1) "a,b")
        then
          bind ignored from perform (FS::append-lines (path "/lines.txt")
            (P::cons String "c" (P::nil String)))
          then
            bind lines2 from perform (FS::read-lines (path "/lines.txt")) then
              perform (A::assert-eq String (S::join-with "," lines2) "a,b,c")
            end
          end
        end
      end
    end
  end

  define transparent check-walk as compute
    bind entries from perform (FS::walk (path "/src")) then
      sequence
        (A::assert-eq Boolean
          (contains-path (path "/src/file.txt") entries)
          true)
        (A::assert-eq Boolean
          (contains-path (path "/src/sub") entries)
          true)
        (A::assert-eq Boolean
          (contains-path (path "/src/sub/nested.txt") entries)
          true)
        (A::assert-eq Boolean
          (find-is-directory (path "/src/sub") entries)
          true)
        (A::assert-eq Boolean
          (find-is-directory (path "/src/file.txt") entries)
          false)
      end
    end
  end

  define transparent check-copy-tree as compute
    bind ignored from perform (FS::copy-tree (path "/src") (path "/dest")) then
      bind exists from perform (FS::path-exists (path "/dest/sub/nested.txt")) then
        bind ignored from perform (A::assert-eq Boolean exists true) then
          bind contents from perform (IO::read-file (path "/dest/sub/nested.txt")) then
            perform (A::assert-eq String contents "nested")
          end
        end
      end
    end
  end

  define transparent main as compute
    sequence
      setup-tree
      check-lines
      check-walk
      check-copy-tree
    end
  end
end
