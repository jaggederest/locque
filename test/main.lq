import prelude as P
# Main test entry point
# Runs all library rollups

import arithmetic as Ar
import assert as A
import cli as CLI
import io as IO
import list as L
import natural as N
import string as S
import time as Time
import typeclass::order as Order

import test::helpers as TH
import test::project as test_project
import test::prelude as test_prelude
import test::arithmetic as test_arithmetic
import test::list as test_list
import test::pair as test_pair
import test::natural as test_natural
import test::assert as test_assert
import test::string as test_string
import test::io as test_io
import test::time as test_time
import test::file as test_file
import test::cli as test_cli
import test::compile as test_compile
import test::path as test_path
import test::examples as test_examples
import test::option as test_option
import test::result as test_result
import test::either as test_either
import test::dictionary as test_dictionary
import test::effects as test_effects
import test::signal as test_signal
import test::net as test_net
import test::http as test_http
import test::shell as test_shell
import test::testimport as test_testimport
import test::typeclass as test_typeclass
import test::type_aliases as test_type_aliases
import test::logic as test_logic
import test::typecheck as test_typecheck
import test::syntax as test_syntax
import test::tools as test_tools
import test::new_syntax_test as test_new_syntax_test
import test::process as test_process

module test::main contains
  define transparent suites as
    [
      (P::pair String (computation Unit) "test_project" test_project::main)
    , (P::pair String (computation Unit) "test_prelude" test_prelude::main)
    , (P::pair String (computation Unit) "test_arithmetic" test_arithmetic::main)
    , (P::pair String (computation Unit) "test_list" test_list::main)
    , (P::pair String (computation Unit) "test_pair" test_pair::main)
    , (P::pair String (computation Unit) "test_natural" test_natural::main)
    , (P::pair String (computation Unit) "test_assert" test_assert::main)
    , (P::pair String (computation Unit) "test_string" test_string::main)
    , (P::pair String (computation Unit) "test_io" test_io::main)
    , (P::pair String (computation Unit) "test_time" test_time::main)
    , (P::pair String (computation Unit) "test_file" test_file::main)
    , (P::pair String (computation Unit) "test_cli" test_cli::main)
    , (P::pair String (computation Unit) "test_compile" test_compile::main)
    , (P::pair String (computation Unit) "test_path" test_path::main)
    , (P::pair String (computation Unit) "test_examples" test_examples::main)
    , (P::pair String (computation Unit) "test_option" test_option::main)
    , (P::pair String (computation Unit) "test_result" test_result::main)
    , (P::pair String (computation Unit) "test_either" test_either::main)
    , (P::pair String (computation Unit) "test_dictionary" test_dictionary::main)
    , (P::pair String (computation Unit) "test_effects" test_effects::main)
    , (P::pair String (computation Unit) "test_signal" test_signal::main)
    , (P::pair String (computation Unit) "test_net" test_net::main)
    , (P::pair String (computation Unit) "test_http" test_http::main)
    , (P::pair String (computation Unit) "test_shell" test_shell::main)
    , (P::pair String (computation Unit) "test_testimport" test_testimport::main)
    , (P::pair String (computation Unit) "test_typeclass" test_typeclass::main)
    , (P::pair String (computation Unit) "test_type_aliases" test_type_aliases::main)
    , (P::pair String (computation Unit) "test_logic" test_logic::main)
    , (P::pair String (computation Unit) "test_typecheck" test_typecheck::main)
    , (P::pair String (computation Unit) "test_syntax" test_syntax::main)
    , (P::pair String (computation Unit) "test_tools" test_tools::main)
    , (P::pair String (computation Unit) "test_new_syntax_test" test_new_syntax_test::main)
    , (P::pair String (computation Unit) "test_process" test_process::main)
    ]

  define transparent suite-action as
    function entry (Pair String (computation Unit)) returns (computation Unit) value
      match entry of-type (Pair String (computation Unit)) as ignored returns (computation Unit)
        case Pair::pair with ignored String action (computation Unit) as action
      end
    end

  define transparent suite-name as
    function entry (Pair String (computation Unit)) returns String value
      match entry of-type (Pair String (computation Unit)) as ignored returns String
        case Pair::pair with name String ignored (computation Unit) as name
      end
    end

  define transparent suite-action-checked as
    function entry (Pair String (computation Unit)) returns (computation Unit) value
      match entry of-type (Pair String (computation Unit)) as ignored returns (computation Unit)
        case Pair::pair with ignored String action (computation Unit) as
          TH::assert-no-output Unit action
      end
    end

  define transparent suite-start-line as
    function name String returns String value
      S::concat "start " name
    end

  define transparent suite-end-line as
    function name String assertions Natural returns String value
      S::concat "end "
        (S::concat name
          (S::concat " (assertions: "
            (S::concat (N::to-string assertions) ")")))
    end

  define transparent format-duration as
    function micros Natural returns String value
      match (Order::less-than Natural micros 1000) of-type Boolean as ignored returns String
        case Boolean::true as
          S::concat (N::to-string micros) "us"
        case Boolean::false as
          match (Order::less-than Natural micros 1000000) of-type Boolean as ignored returns String
            case Boolean::true as
              S::concat (N::to-string (Ar::div-nat micros 1000)) "ms"
            case Boolean::false as
              S::concat (N::to-string (Ar::div-nat micros 1000000)) "s"
          end
      end
    end

  define transparent timing-line as
    function entry (Pair String Natural) returns String value
      match entry of-type (Pair String Natural) as ignored returns String
        case Pair::pair with name String elapsed Natural as
          S::concat "timing "
            (S::concat name
              (S::concat ": " (format-duration elapsed)))
      end
    end

  define transparent summary-line as
    function assertions Natural suites Natural returns String value
      S::concat "summary: "
        (S::concat (N::to-string assertions)
          (S::concat " assertions, "
            (S::concat (N::to-string suites) " suites")))
    end

  define transparent run-suite-verbose as
    function entry (Pair String (computation Unit)) returns (computation (Pair String Natural)) value
      match entry of-type (Pair String (computation Unit)) as ignored returns (computation (Pair String Natural))
        case Pair::pair with name String action (computation Unit) as
          compute
            bind ignored from perform (IO::print (suite-start-line name)) then
              bind before from perform A::assertion-count then
                bind measured from perform (Time::measure Unit (TH::assert-no-output Unit action)) then
                  bind after from perform A::assertion-count then
                    bind ignored from perform
                      (IO::print
                        (let value assertions be Ar::subtract after before in
                          suite-end-line name assertions
                        end)) then
                      return (P::pair String Natural name (P::snd Unit Natural measured))
                    end
                  end
                end
              end
            end
          end
      end
    end

  define transparent run-tests as compute
    perform (P::sequence-unit
      (P::map (Pair String (computation Unit)) (computation Unit) suite-action suites))
  end

  define transparent measure-suite as
    function entry (Pair String (computation Unit)) returns computation (Pair String Natural) value
      match entry of-type (Pair String (computation Unit)) as ignored returns computation (Pair String Natural)
        case Pair::pair with name String action (computation Unit) as
          compute
            bind measured from perform (Time::measure Unit (TH::assert-no-output Unit action)) then
              return (P::pair String Natural name (P::snd Unit Natural measured))
            end
          end
      end
    end

  define transparent measure-suites as
    function xs (List (Pair String (computation Unit))) returns computation (List (Pair String Natural)) value
      P::fold-compute (Pair String (computation Unit)) (List (Pair String Natural))
        (function acc (computation (List (Pair String Natural)))
                  entry (Pair String (computation Unit))
        returns computation (List (Pair String Natural)) value
          compute
            bind results from perform acc then
              bind measured from perform (measure-suite entry) then
                return (P::cons (Pair String Natural) measured results)
              end
            end
          end
        end)
        (compute return (of-type [] (List (Pair String Natural))) end)
        xs
    end

  define transparent compare-suites as
    function a (Pair String Natural) b (Pair String Natural) returns Boolean value
      Order::greater-than Natural (P::snd String Natural a) (P::snd String Natural b)
    end

  define transparent suite-line as
    function entry (Pair String Natural) returns String value
      match entry of-type (Pair String Natural) as ignored returns String
        case Pair::pair with name String elapsed Natural as
          S::concat name (S::concat ": " (S::concat (N::to-string elapsed) "us"))
      end
    end

  define transparent report-slowest as
    function results (List (Pair String Natural)) returns computation Unit value
      let value sorted be L::sort-by (Pair String Natural) compare-suites results in
        let value top be L::take (Pair String Natural) 5 sorted in
          let value lines be P::map (Pair String Natural) String suite-line top in
            compute
              bind ignored from perform (IO::print "Slowest test suites (us):") then
                bind ignored from perform (P::sequence-unit
                  (P::map String (computation Unit) IO::print lines)) then
                  return Unit::tt
                end
              end
            end
          end
        end
      end
    end

  define transparent run-tests-slow as compute
    bind results from perform (measure-suites suites) then
      perform (report-slowest results)
    end
  end

  define transparent slow-enabled as
    function args (List String) returns Boolean value
      L::member String "--slow" args
    end

  define transparent verbose-enabled as
    function args (List String) returns Boolean value
      L::member String "--verbose" args
    end

  define transparent run-tests-verbose as compute
    bind results from perform
      (P::sequence (Pair String Natural)
        (P::map (Pair String (computation Unit)) (computation (Pair String Natural)) run-suite-verbose suites)) then
      bind assertions from perform A::assertion-count then
        bind ignored from perform
          (IO::print (summary-line assertions (P::length-list (Pair String Natural) results))) then
          bind ignored from perform
            (P::sequence-unit
              (P::map String (computation Unit) IO::print
                (P::map (Pair String Natural) String timing-line results))) then
            return Unit::tt
          end
        end
      end
  end

  define transparent main as compute
    bind args from perform CLI::args then
      perform
        (match (slow-enabled args) of-type Boolean as ignored returns (computation Unit)
          case Boolean::true as run-tests-slow
          case Boolean::false as
            match (verbose-enabled args) of-type Boolean as ignored returns (computation Unit)
              case Boolean::true as run-tests-verbose
              case Boolean::false as TH::assert-no-output Unit run-tests
            end
        end)
    end
  end
end
